module m_tecpost3D
contains
subroutine tecpost3D(irepf,irepl,fname,dname)
! Extracts data from restart file and produces tecplot output
   use mod_states
   use m_eclipse_read
   use m_ensio
   use m_getmask
   use m_togrid
   implicit none
   
   type(states4) mem4
   type(states) mem


   integer,           intent(in) :: irepf
   integer,           intent(in) :: irepl
   character(len=80), intent(in) :: dname
   character(len=80), intent(in) :: fname

   character(len=80), allocatable :: filename(:)


   integer nrfields,nrrep
   integer, parameter :: maxfields=100
   character(len=80)  field(maxfields)
   character(len=4)   cnum

   real work3D(nx,ny,nz)
   real twod(0:nx+1,0:ny+1)

! Variables for reading coordinate positions from ECLIPSE.FGRID
   character(len=13) :: basename='ECLIPSE.FGRID'
   integer,   allocatable :: DIMENS(:)
   character(len=10), allocatable :: RADIAL(:)
   character(len=10), allocatable :: GRIDUNIT(:)
   character(len=10), allocatable :: MAPUNITS(:)
   real*4, allocatable :: MAPAXES(:)

   character(len=8)        gridname(1)
   integer                 gridsize(1)
   character(len=4)        gridtype(1)
   logical ex

   integer cc(7)
   integer coords(7,nx*ny*nz)
   real*4 corners4(24)
   real corners(24,nx*ny*nz)



   real xpos(nx,ny,nz),ypos(nx,ny,nz),zpos(nx,ny,nz)

   integer iw(100),jw(100),kw(100),nrnodes
   character(len=50)wellname

   integer i,j,k,l,m,iflg,ifile,ifld,ird


   nrrep=irepl-irepf+1
   allocate(filename(nrrep))
   j=0
   do i=irepf,irepl
      j=j+1
      write(cnum,'(i4.4)')i
      filename(j)=trim(dname)//trim(fname)//cnum//'.uf'
      inquire(file=trim(filename(j)),exist=ex)
      if (.not.ex) then
         print '(2a)','file do not exist : ',trim(filename(j))
         j=j-1
      else
         print '(2a)','processing files  : ',trim(filename(j))
      endif
   enddo
   nrrep=j
   if (nrrep==0) then
      print *,'No files to process.'
      return
   endif



   field(:)(:)=' '
   i=1;   field(i)(:)='PRES'
   i=i+1; field(i)(:)='PERMX'
   i=i+1; field(i)(:)='PERMZ'
   i=i+1; field(i)(:)='PORO'
#ifdef GAUSS
   i=i+1; field(i)(:)='GAUSS1'
   i=i+1; field(i)(:)='GAUSS2'
#endif
   i=i+1; field(i)(:)='SOIL'
   i=i+1; field(i)(:)='SGAS'
   i=i+1; field(i)(:)='SWAT'
   i=i+1; field(i)(:)='RS'
#ifdef VAPOIL
   i=i+1; field(i)(:)='RV'
#endif
   nrfields=i

   do i=1,nrfields
      print '(2a)','Including fields: ',trim(field(i))
   enddo

   open(10,file='Refcase/ECLIPSE.FGRID',status='old',form='formatted',access='sequential')
   m=0
   do
      read(10,'(t3,a8,t13,i11,t26,a4)',end=999)gridname(1),gridsize(1),gridtype(1)
      select case (trim(gridname(1)))
      case('DIMENS')
         allocate(DIMENS(gridsize(1)))
         ird=read_integer(gridsize(1),DIMENS,gridtype(1))
      case('GRIDUNIT')
         allocate(GRIDUNIT(gridsize(1)))
         ird=read_char(gridsize(1),GRIDUNIT,gridtype(1))
      case('MAPAXES')
         allocate(MAPAXES(gridsize(1)))
         ird=read_real(gridsize(1),MAPAXES,gridtype(1))
      case('MAPUNITS')
         allocate(MAPUNITS(gridsize(1)))
         ird=read_char(gridsize(1),MAPUNITS,gridtype(1))
      case('RADIAL')
         allocate(RADIAL(gridsize(1)))
         ird=read_char(gridsize(1),RADIAL,gridtype(1))
      case('COORDS')
         m=m+1
         ird=read_integer(gridsize(1),cc(:),gridtype(1))
         coords(:,m)=cc(:)
      case('CORNERS')
         ird=read_real(gridsize(1),corners4(:),gridtype(1))

         i=coords(1,m)
         j=coords(2,m)
         k=coords(3,m)

         xpos(i,j,k)=sum(corners4(1:22:3))/8.0
         ypos(i,j,k)=sum(corners4(2:23:3))/8.0
         zpos(i,j,k)=sum(corners4(3:24:3))/8.0

         corners(:,i)=corners4(:)

      case default
         print '(t3,a8,t13,i11,t26,a4)',gridname(1),gridsize(1),gridtype(1)
         print *,'iniens: variable not found '
         stop
      end select

      if (m==nx*ny*nz) exit

   enddo
 999  close(10)



   do ifile=1,nrrep
      inquire(file=trim(filename(ifile)),exist=ex)
      if (ex) then
         iflg=open_ensemblefile(filename(ifile),'old')
         iflg=read_ensmem(1,mem4)
         mem=mem4
      endif
      close(10)
      print *,'tecpost: processing file: ',trim(filename(ifile))

      if (ifile == 1) then
         open(11,file=trim(dname)//'tec'//trim(fname)//'.dat')
         write(11,'(a,a3,a)') 'TITLE = "Generated by tecpost"'
         write(11,'(a)')'VARIABLES = "I-index" "J-index" "k-index" "xpos" "ypos" "zpos"'
         do j=1,nrfields
            write(11,'(a,a,a)',advance='no')'"',trim(field(j)),'" '
            if (mod(j,5)==0) write(11,'(a1)')' '
         enddo

         write(11,103)trim(filename(ifile)),nx,ny,nz
     103 format('ZONE T="',a,'", F=BLOCK,',' I=',I4,' ,J=',I4,' ,K=',I4)

         write(11,'(30i5)')(((i,i=1,nx),j=1,ny),k=1,nz)
         write(11,'(30i5)')(((j,i=1,nx),j=1,ny),k=1,nz)
         write(11,'(30i5)')(((k,i=1,nx),j=1,ny),k=1,nz)
         write(11,'(10(1x,g15.7))')ypos
         write(11,'(10(1x,g15.7))')xpos
         write(11,'(10(1x,g15.7))')-zpos

      else
         write(11,103)trim(filename(ifile)),nx,ny,nz
         write(11,'(a)',advance='no')'D=(1,2,3,4,5,6)'
      endif

      do ifld=1,nrfields
         select case (trim(field(ifld)))
         case ('PRES')
            work3D=togrid(nx,ny,nz,mask,mem%pres)
         case ('PERMX')
            work3D=0.0
            work3D=unpack(mem%permx,lmask,work3D)
         case ('PERMZ')
            work3D=0.0
            work3D=unpack(mem%permz,lmask,work3D)
         case ('PORO')
            work3D=0.0
            work3D=unpack(mem%poro,lmask,work3D)
#ifdef GAUSS2
         case ('GAUSS1')
            work3D=mem%gauss1
         case ('GAUSS2')
            work3D=mem%gauss2
#endif
         case ('SOIL')
            work3D=togrid(nx,ny,nz,mask,1.0-mem%sg-mem%sw)
         case ('SGAS')
            work3D=togrid(nx,ny,nz,mask,mem%sg)
         case ('SWAT')
            work3D=togrid(nx,ny,nz,mask,mem%sw)
         case ('RS')
            work3D=togrid(nx,ny,nz,mask,mem%rs)
#ifdef VAPOIL
         case ('RV')
            work3D=togrid(nx,ny,nz,mask,mem%rv)
#endif
         case default
            print *,'tecpost: no match to ',trim(field(ifld)),' in select'
            stop
         end select

         write(11,'(10(1x,g15.7))')work3D

      enddo

   enddo


   open(10,file='wellgeo.dat')
      do l=1,50
         read(10,'(a)',end=998)wellname
         read(10,*)nrnodes
         do i=1,nrnodes
            read(10,*)iw(i),jw(i),kw(i)
         enddo

         write(11,'(a)')'GEOMETRY X=0, Y=0, Z=0, CS=GRID, LT=0.8 C=BLACK, T=LINE3D, F=POINT '
         write(11,'(a)')'1'
         write(11,'(i3)')nrnodes+1
         write(11,'(10(1x,g15.7))')ypos(iw(1),jw(1),kw(1)), xpos(iw(1),jw(1),kw(1)), -zpos(iw(1),jw(1),kw(1))+100.0
         do i=1,nrnodes
            write(11,'(10(1x,g15.7))')ypos(iw(i),jw(i),kw(i)), xpos(iw(i),jw(i),kw(i)), -zpos(iw(i),jw(i),kw(i))
         enddo
         write(11,*)

      enddo


   998 close(10)

   print *,'tecplot3D done....'

end subroutine
end module

